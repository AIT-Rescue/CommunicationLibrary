apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'

//チーム名です。コンパイル時に、Jarのファイル名になります。デフォルトでは、プロジェクトのディレクトリ名になります。
String teamName = name

//Jarを読み込むための記述です。Teamを実装したクラスを、パッケージを含めて記述してください。
String teamClassName = 'comlib.adk.sample.SampleTeam'

//バージョンの記述です。設定すると、Jarの名前が、[teamName]-[version].jarになります。
//version = '1.0.0'

///////////////////////////////////////////////////////////////////////////////////////////////////

repositories {
    jcenter()
}

dependencies {
    compile fileTree(dir: 'library/util', include: '*.jar')
    compile fileTree(dir: 'library/rescue/core', include: '*.jar')
    compile fileTree(dir: 'library/rescue/util', include: '*.jar')
}

sourceSets {
    main.java.srcDirs = ['src/java']
    main.resources.srcDirs = ['src/resources']
}

jar {
    destinationDir = new File(projectDir, 'tactics')
    baseName = teamName
    manifest.attributes('Team-Class': teamClassName)
}

[compileJava, compileTestJava].each {
    it.options.encoding = 'UTF-8'
    it.options.compilerArgs += ['-source', '1.7', '-target', '1.7']
}
javadoc.options.encoding = 'UTF-8'

task start (type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = "comlib.adk.launcher.Main"
    //-Pargs="-s:localhost:7000 -t:sample:-1:-1:-1"
    if (project.hasProperty('args')) {
        args project.args.split('\\s+')
    }
    //maxHeapSize = '4096m'
    jvmArgs '-Xms512m', '-Xmx4g'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.1'
}
