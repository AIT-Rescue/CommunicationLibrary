apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'


jar.baseName = 'comlib-adk'
//version = '0.3.0'

repositories {
    jcenter()
}

dependencies {
    compile fileTree(dir: new File(rootDir, 'library/util'), include: '*.jar')
    compile fileTree(dir: new File(rootDir, 'library/rescue/core'), include: '*.jar')
    compile fileTree(dir: new File(rootDir, 'library/rescue/util'), include: '*.jar')
    compile project(':Comlib')
    testCompile 'junit:junit:4.11'
}

[compileJava, compileTestJava].each {
    it.options.encoding = 'UTF-8'
    it.options.compilerArgs += ['-source', '1.7', '-target', '1.7']
}
javadoc.options.encoding = 'UTF-8'

build.mustRunAfter 'clean'

task makeArchive(type: Zip, dependsOn: [clean, build]) {
    destinationDir = new File(buildDir, 'libs')
    archiveName = jar.baseName + '-src.zip'
    into('comlib') {
        from 'src/main/java/comlib'
    }
}

task start (type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = "comlib.adk.launcher.Main"
    if (project.hasProperty('args')) {
        args project.args.split('\\s+')
    }
    jvmArgs '-Xms512m', '-Xmx4g'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.1'
}
